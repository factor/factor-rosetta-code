! Ludic numbers are related to prime numbers as they are generated by a
! sieve quite like the Sieve of Eratosthenes is used to generate prime
! numbers.
! 
! The first ludic number is 1.
! 
! To generate succeeding ludic numbers create an array of increasing
! integers starting from 2.
! 
!     2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26 ...
! 
! (Loop)
! 
! -   Take the first member of the resultant array as the next ludic
!     number 2.
! -   Remove every 2^(nd) indexed item from the array (including the
!     first).
! 
!         2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26 ...
! 
! -   (Unrolling a few loops...)
! -   Take the first member of the resultant array as the next ludic
!     number 3.
! -   Remove every 3^(rd) indexed item from the array (including the
!     first).
! 
!         3 5 7 9 11 13 15 17 19 21 23 25 27 29 31 33 35 37 39 41 43 45 47 49 51 ...
! 
! -   Take the first member of the resultant array as the next ludic
!     number 5.
! -   Remove every 5^(th) indexed item from the array (including the
!     first).
! 
!         5 7 11 13 17 19 23 25 29 31 35 37 41 43 47 49 53 55 59 61 65 67 71 73 77 ...
! 
! -   Take the first member of the resultant array as the next ludic
!     number 7.
! -   Remove every 7^(th) indexed item from the array (including the
!     first).
! 
!         7 11 13 17 23 25 29 31 37 41 43 47 53 55 59 61 67 71 73 77 83 85 89 91 97 ...
! 
! -    ...
! -   Take the first member of the current array as the next ludic number
!     L.
! -   Remove every L^(th) indexed item from the array (including the
!     first).
! -    ...
! 
! Task:
! 
! -   Generate and show here the first 25 ludic numbers.
! -   How many ludic numbers are there less than or equal to 1000?
! -   Show the 2000..2005^(th) ludic numbers.
! 
! Stretch goal:
! 
! Show all triplets of ludic numbers < 250.
! 
! -   A triplet is any three numbers x, x + 2, x + 6 where all three
!     numbers are also ludic numbers.

USING: formatting fry kernel make math math.ranges namespaces
prettyprint.config sequences sequences.extras ;
IN: rosetta-code.ludic-numbers

: next-ludic ( seq -- seq' )
    dup first '[ nip _ mod zero? not ] filter-index ;

: ludics-upto-2005 ( -- a )
    22,000 2 swap [a,b] [ ! 22k suffices to produce 2005 ludics
        1 , [ building get length 2005 = ]
        [ dup first , next-ludic ] until drop
    ] { } make ;

: ludic-demo ( -- )
    100 margin set ludics-upto-2005
    [ 6 tail* ] [ [ 1000 < ] count ] [ 25 head ] tri
    "First 25 ludic numbers:\n%u\n\n"
    "Count of ludic numbers less than 1000:\n%d\n\n"
    "Ludic numbers 2000 to 2005:\n%u\n" [ printf ] tri@ ;

MAIN: ludic-demo
